group 'com.jmpeax'
version '0.1.0-SNAPSHOT'
description "Simple Http Client"

buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        // Add dependency for build script,
        // so we can access Git from our
        // build script.
        classpath 'org.ajoberstar:grgit:1.1.0'
        classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0"
    }
}

apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: "jacoco"
apply plugin: 'findbugs'
apply plugin: 'jdepend'
apply plugin: 'osgi'
apply plugin: "com.github.hierynomus.license"




ext {
    // Open the Git repository in the current directory.
    git = org.ajoberstar.grgit.Grgit.open(dir:".")
    // Get commit id of HEAD.
    revision = git.head().id
    // Alternative is using abbreviatedId of head() method.
    // revision = git.head().abbreviatedId
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

checkstyle {
    configFile = rootProject.file('checkstyle.xml')
    toolVersion ="7.7"
}
test {
    // enable TestNG support (default is JUnit)
    useTestNG()
}
sourceSets {
    test {
        java.srcDir 'src/test/unit/java'
        resources.srcDir 'src/test/unit/resources'
    }
    integration {
        java.srcDir 'src/test/integration/java'
        resources.srcDir 'src/test/integration/resources'
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integrationCompile.extendsFrom testCompile
    integrationRuntime.extendsFrom testRuntime
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
    testClassesDir = sourceSets.integration.output.classesDir
    classpath = sourceSets.integration.runtimeClasspath
    useTestNG()// enable TestNG support (default is JUnit)
}

dependencies {
    testCompile  group: 'org.testng', name: 'testng', version: '6.11'
    testCompile  group: 'org.mockito', name: 'mockito-core', version: '2.8.9'
}

jar{
    manifest{
        attributes(
                 "BuildId": "${revision}"
        )
    }
}
